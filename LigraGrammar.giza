
//import SolusGrammar;

commands = ( command ';' )* command ';'? ;

interval = (
    varref '=' '[' expr '..' expr ']' |
    expr ( '<' | '<=' ) varref ( '<' | '<=' ) expr
);

command = (
    help-command |
    clear-command |
    show-command |
//    user-command |
    plot-command |
    paint-command |
    del-command |
    var-assign-command |
    func-assign-command
);

help-command = 'help' help-item?;
clear-command = 'clear' 'all'?;
show-command = 'show' ( 'history' | 'vars' | 'variables' | 'constants' );
plot-command = 'plot' expr (',' expr)* 'for' interval ( ',' interval )* ;
paint-command = 'paint' expr 'for' interval ',' interval ;
del-command = 'del' varref;
var-assign-command = varref ':=' expr ;
func-assign-command = identifier '(' ( identifier ( ',' identifier )* )? ')' ':=' expr ;

help-item = identifier;

//user-command = identifier expr*;


<comment>
comment = ( '/*' ( [^*] | '*'+ [^*/] )* '*'+ '/' |
            '//' [^\n]* '\n' );

/////////////////////////////


expr = subexpr ( binop subexpr )*;

function-call = identifier:name '(' ( expr:arg ( ',' expr:arg )* )? ')';

<ignore case, token> number = (
    '0b' [01]+ | 
    '0o' [01234567]+ | 
    '0x' [\dabcdef]+ | 
     float-number
);

// non-atomic, usable by the spanner
<ignore case, subtoken> float-number = [+-]? [\d]+ ( '.' [\d]+ )? ( [eE] [+-]? [\d]+ )? ;

<token> string = (
    '\'' ( [^\\'] | '\\' [rnt\\'] | unicodechar )+ '\'' |
    '"' ( [^\\"] | '\\' [rnt\\"] | unicodechar )+ '"'
);

<ignore case, subtoken> unicodechar = '\\x' [\dabcdef][\dabcdef][\dabcdef][\dabcdef];

<token> binop = [+-*/%^&|];

unary-op = [+-] (
    paren |
    function-call |
    string |
    unary-op |
    varref
);

subexpr = (
    paren |
    function-call |
    number |
    string |
    unary-op |
    varref
);

paren = '(' expr ')';

varref = identifier;

<token> identifier = [\w] [\d\w_]*;

